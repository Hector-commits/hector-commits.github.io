---
title: Personal Notes on MB-820
date: 2024-12-10 16:00:00 000
categories: [Business Central]
tags: [Business Central]
published: false
---

# Assisted Setup

When you build an extension for Business Central, and you want the user to provide some basic settings for working with your solution, then you can use an assisted setup.

Assisted setup guides the user through the setup process to install or configure your solution. Any solution that you provide directly or from AppSource should provide users with a smooth installation experience.

An assisted setup, also called a wizard, is a page that consists of one or more user input pages or steps. These steps are linked together, enabling users to carry out infrequently performed tasks, such as configuration or specific business tasks.

To use an assisted setup in your application, you need to integrate your solution in the Assisted Setup table. Therefore, you have to build a wizard page that guides the user through the installation process.

To start a wizard through Assisted Setup, you can use the search menu and search for assisted setup.

![AssistedSetup](/images/202411/AssistedSetup.png)

The Assisted Setup page contains several links that you can select to start a wizard. For example, one links helps you set up email, another can help you migrate business data, while another can help you set up and configure your Business Inbox in Outlook. The Assisted Setup page is also where you can view which setups have been completed and which haven't.

![AssistedSetups](/images/202411/AssistedSetups.png)

## Assisted Setup - Create a wizard page

A wizard page guides a user through the installation process. The wizard page is called from within Assisted Setup. To build a wizard page, you need to set the **PageType** property to **NavigatePage**.

In the cliente, the **NavigatePage** page doesn't have an action bar. It's designed to have buttons at the bottom that let users move back and forth through the steps by selecting the Back and Next buttons, until they finish by selecting the Finish button.

![NavigatePage](/images/202411/NavigatePage.png)

The purpose of a wizard is to enable a user to configure settings and store those settings only when the user selects the Finish button. Because Business Central automatically stores all modifications from the moment you leave an input field, you need to set the **SourceTableTemporary** property to true. This setting ensures that modifications are only stored in memory and that, when the user selects the Finish button, you can copy the modifications from memory to the database.

### Assisted Setup - Build the assisted setup

This section describes the basic tasks for creating an assisted setup. It gives you an understanding of the general structure of an assisted setup guide. Knowing this information helps you build assisted setup guides that look and feel like the ones provided by Microsoft. This section includes some basic AL code for the various steps involved in createing an assisted setup guide.

The example code for reference only, and your implementation might vary.

1. Create a page for the assisted setup, for example:

```AL
page 50100 ToDoAssistedSetup
{
PageType = NavigatePage;
SourceTable = "To-do";
SourceTableTemporary = true;
Caption = 'Add a To-Do for the Team';
}
```

2. Set the following properties:

* **PageType property** - Set to NavigatePage.
* **SourceTable** - Set to the name of the table that stores the data for the assisted setup.
* **SourceTableTemporary** - Set to true. The reason is because Business Central automatically stores all modifications to the database tables as soon as users move focus to another field or close the page. Using a temporary table let's users exit the assisted setup guide at any point, without saving the changes they've made so far to the database. Instead, the data modifications are only stored in memory, until code is run to transfer the data to the database.
* **Caption** - Set to the title that you want to show on the top of each step of the assisted guide.

3. Add the steps that comprise the assisted setup.

For each step that you want in the guide add a `group()` control to the root-level of the `layout > area(Content)` control. For example, the following code adds three steps:

```AL
layout
{
    area(Content)
    {
        group(Step1)
        {
            Caption = '';
            InstructionalText = '';
            Visibility = Step1Visible;

        }
        group(Step2)
        {
            Caption = '';
            InstructionalText = '';
            Visibility = Step2Visible;

            field(Field2; "Field2")
            {
                ApplicationArea = All;
                Caption = '';
            }
        }
        group(Step3)
        {
            Caption = '';
            InstructionalText = '';
            Visibility = Step3Visible;
        }
    }
}

var
    Step1Visible: Boolean;
    Step2Visible: Boolean;
    Step3Visible: Boolean;
    Step: Option Start,Fill,Finish;

local procedure EnableControls()
begin
    ResetControls();
    case Step of
        Step::Start:
            ShowStep1();
        Step::Fill:
            ShowStep2();
        Step::Finish:
            ShowStep3();
    end;
end;
```
The individual `group()` controls define the content to display for the step, like text and data entry fields. For example, use the Caption and InstructionalText properties to add text. 

The order of the `group()` controls doesn't necessarily determine the order of the steps in the assisted setup. You add AL code to define the logic for when each step appears.

You can include `group()` controls within the root-level `group()` controls. These subgroups can be useful for adjusting the layout fields on a step  and adding captions and instructional text.

Because only one step can be shown at a time, you have to add logic to control when each step is shown. To control the visibility:

* For each step, define a global Boolean variable or a step number condition and apply it the group's Visible property. The example uses the Boolean variables Step1Visible, Step2Visible, and Step3Visible.
* You then have to add AL code to change these variables depending on which step the user is working on.
* Define a global option variable that has a value for each step.
* You use this variable in code to track which step is active.

Multiline fields shown inside a step are shown without a gray background. This behavior allows you to write a list of items or any formatted text that requires new lines.

4. Add the navigation buttons.

Assisted setups typically have a Back, Next, and Finish button at the bottom-right of the page. Add each button by using `action()` control.

```al

actions
{
    area(Processing)
    {
        action(Back)
        {
            Enabled = BackEnable;
            InFooterBar = true;
            Image = PreviousRecord;

            trigger OnAction()
            begin
                NextStep(true);
            end;
        }
        action(Next)
        {
            Enabled = NextEnable;
            InFooterBar = true;
            Image = NextRecord;

            trigger OnAction()
            begin
                NextStep(false);
            end;
        }
        action(Finish)
        {
            Enabled = FinishEnable;
            InFooterBar = true;
            Image = Approve;

            trigger OnAction()
            begin
                Finished();
            end;
        }
    }
}

var
    BackEnable: Boolean;
    NextEnable: Boolean;
    FinishEnable: Boolean;

local procedure NextStep(Backwards: Boolean)
begin
    if Backwards then
        Step := Step - 1
    else
        Step := Step + 1;
    EnableControls();
end;

local procedure Finished()
begin
    StoreRecordVar();
    CurrPage.Close();
end;

```
* Set the **InFooterBar** property for each to true.
* If the property isn't set or is false, then the actions don't appear.
* Similar to the visible property on the groups, use the Enabled property to control when buttons are active on the different steps. For example, Back isn't active in the first step, and Next isn't active in the last step.
* Like on groups, define global Boolean variables for each action, then apply the variable to the action's Enabled property. The global variables are then controlled in AL code.
* To style the buttons to match other assisted setups in the Microsoft base application, set the Image property to PreviousRecord for the back button, NextRecord for the next button, and Approve for the Finish.

5. Transfer data from the temporary table to the database table. For example:

```al
var
     ToDoRec: Record "To-do";

 local procedure StoreRecordVar();
 begin
     ToDoRec.TransferFields(Rec, true);
     ToDoRec.Insert();
 end;
```

### Assisted Setup - Add standard banners

You might have noticed that most assisted setup guides in the base application, and in this article, include two different banners under the step caption: a gear and a check mark. These banners are based on the following image files in the Media Repository table:

* AssistedSetup-NoText-400px.png for the gear.
* AssistedSetupDone-NoText-400px.png for the check mark.

You can reuse these images in your custom assisted setup guides to provide a consistent look. To display the banners, add two root-level group() controls in to the area(Content) control, like for the steps. Use field() controls for displaying the gear and check mark images. The following snippets illustrate one way of adding the banners.

```AL
...
    layout
    {
        area(Content)
        {
            ...
            group(StandardBanner)
            {
                Caption = '';
                Editable = false;
                Visible = TopBannerVisible and not FinishActionEnabled;
                field(MediaResourcesStandard; MediaResourcesStandard."Media Reference")
                {
                    ApplicationArea = All;
                    Editable = false;
                    ShowCaption = false;
                }
            }
            group(FinishedBanner)
            {
                Caption = '';
                Editable = false;
                Visible = TopBannerVisible and FinishActionEnabled;
                field(MediaResourcesDone; MediaResourcesDone."Media Reference")
                {
                    ApplicationArea = All;
                    Editable = false;
                    ShowCaption = false;
                }
            }
        }
    }
...
var
    ...
    MediaRepositoryDone: Record "Media Repository";
    MediaRepositoryStandard: Record "Media Repository";
    MediaResourcesDone: Record "Media Resources";
    MediaResourcesStandard: Record "Media Resources";

trigger OnInit();
begin
    ...
    LoadTopBanners();
end;

local procedure LoadTopBanners();
begin
    if MediaRepositoryStandard.Get('AssistedSetup-NoText-400px.png', Format(CurrentClientType())) and
        MediaRepositoryDone.Get('AssistedSetupDone-NoText-400px.png', Format(CurrentClientType()))
    then
        if MediaResourcesStandard.Get(MediaRepositoryStandard."Media Resources Ref") and
            MediaResourcesDone.Get(MediaRepositoryDone."Media Resources Ref")
    then
            TopBannerVisible := MediaResourcesDone."Media Reference".HasValue();
end;
```

### Assisted Setup - Integrate with Assisted Setup in your extension

From the Dynamics 365 Business Central 2021 release wave 1, the records in Manual Setup and Assisted Setup are migrated to a new table, **Guided Experience Item**. This table has a field, Type that determines the type of each record:

**Assisted Setup -** Where all assisted setup wizards now reside.
**Manual Setup -** Where all manual setup records now reside.
**Learn -** Records of type Learn point to an external URL.
**Tour -** Records of type Tour point to a tour of the role center. Currently, only Microsoft can add these in the Business Centrla platform. We're considering enabling AL developers to add and control these tours in time.
**Spotlight Tour -** Records of type Spotlight Tour point to a special tour where Business Central opens a page in special mode that suppresses other tours and shines a bright spotlight on core capabilities, such as Open in Excel or Share to Teams. Use the spotlight tour in sales and evaluation scenarios to show off key capabilities that get customers excited about the product. Consider how you can use these spotlights for your own or other fatures. The following illustration shows a spotlight tour that calls out Teams and Excel integration on a page.

![SpotlightTour](/images/202411/SpotlightTour.png)

The spotlight tour suppresses teaching tips on the page and immediately calls out Teams and Excel integration features as shown in the following illustration.

![SpotlightSuppresingTeachingTips](/images/202411/SpotlightSuppresingTeachingTips.png)

**Video -** Records of the type Video enables the user to watch a video provided by a custom URL. The video plays in a window inside Business Central. Consider how you can utilize video to explain a feature or capability. Video is normally used in a sales/evaluation scenario but could also be used for training purposes in an onboarding case. The following illustration shows a video player that is started from the checklist.

![VideoRecord](/images/202411/VideoRecord.png)

**Application Feature -** Records of type Application Feature enables a checklist task to open any page inside Business Central. Similar to Manual Setup, it opens a page and displays a page tour if any is defined.

Checklist items can be based on records in the Guided Experience Item table, which means that before you surface a task on the checklist, you must first add it to Guided Experience Item.

To insert a record in the Guided Experience Item table, use the fa√ßade functions in the Guided Experience codeunit.

* InsertManualSetup.
* InsertAssistedSetup.
* InsertLearnLink.
* InsertTour.
* InsertSpotlightTour.
* InsertVideo.
* InsertApplicationFeature.

For example, let's say that you have the page **My ISV Solution Setup** where the user can configure your app. You want to invite the business manager to access this page from the checklist. In this example, you must insert a new record in the Guided Experience Item table with the type Manual Setup and provide the metadata as data (title, descriptions, and so on) as described belo.

After having created this record, it can now be referenced from and inserted into a checklist.

### Assisted Setup - Easily access setup pages for new apps

You can add an option to run a setup page directly from the Extensions Management page if the app has specified a setup page. The developer of the app should provide a reference to the setup page in the app manifest, so that Business Central knows which page to run for the specific app.

In practical terms, tehre are two new features:

* An ability to get an overview of setup pages brought by an app, if the app registers those in the Guided Experience Item table.
* A new action on the Extensions Management page that runs the page that the developer specified.

![ExtManagementSetup](/images/202411/ExtManagementSetup.png)

### Add to the Assisted Setup page

Page 1901 **Assisted Setup** of the base application gives users quick access to all assisted setup guides. This section explains how you can add your assisted setup guide to the page.

The base application includes several objects that control what items appear in the **Assisted Setup** page. For example, table 1990 Guided Experience Item is an internal table that's the source for the Assisted Setup page. Codeunit 1990 Guided Experience is used to register your assisted setup guide in the Guided Experience Item table and run it from the Assisted Setup page. The Guided Experience codeunit published the OnRegisterAssistedSetup event that notifies the Assisted Setup page to add new items.

```AL
[IntegrationEvent(false, false)]
internal procedure OnRegisterAssistedSetup()
begin
end;
```
To add an assisted setup guide to the Assisted Setup page, add a codeunit that subscribes to the OnRegisterAssistedSetup event. The following code illustrtes how you can add the **ToDoAssistedSetup** assisted setup guide. The example also creates a new category called Tasks on the Assisted Setup page to link to **ToDoAssistedSetup** is listed:

```AL
codeunit 50100 "AddToDoAssistedSetup"
{
    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Guided Experience", 'OnRegisterAssistedSetup', '', true, true)]
    local procedure OnRegisterAssistedSetup()
    var
        AssistedSetup: Codeunit "Guided Experience";
        GuidedExperienceType: Enum "Guided Experience Type";
        AssistedSetupGroup: Enum "Assisted Setup Group";
        VideoCategory: Enum "Video Category";
    begin
        if not AssistedSetup.Exists(GuidedExperienceType::"Assisted Setup",
            ObjectType::Page,
            Page::"ToDoAssistedSetup") then
        AssistedSetup.InsertAssistedSetup(
        // Link text for the assisted setup guide
        'Add a to-do',
        // Short description, not shown on page
        'Create a task for your team',
        // Text that shows in Description column
       'Register a task for your team and assign people',
       1,
       ObjectType::Page,
       Page::ToDoAssistedSetup,
       // Assign guide to Task category
       AssistedSetupGroup::Tasks,
       //Video URL not required
       '',
       VideoCategory::Uncategorized,
       //Help URL not required
       '');
    end;
}

enumextension 50100 MyEnumExtension extends "Assisted Setup Group"
{
    value(100; Tasks)
    {
    }
}
```

# Send notifications in Dynamics 365 Business Central

## Work with notifications

Notifications are a way to inform the users,without having to block them, about a situation that takes place. You can use error messages or message statements, but when the messages show up, the user needs to act to complete the process.

With the notification, a user can choose to ignore a notification or look at its details. In the following example, a notification is used to display a message if the available inventory for an item is lower than the entered quantity for a certain location.

![Notification](/images/202411/Notification.png)

When you select the Show details link, another page is displayed with information about the current inventory of this item.

![ShowDetails](/images/202411/ShowDetails.png)

Notifications send non-blocking information to the user; therefore, they are different from the Message function. A message is modal and requires an action before it continues with its process, while a notification is non-modal and does not require immediate action by the user.

Notifications appear in the notification bar at the top pages in Business Central. You can dismiss the notification, or if actions are defined, you can select one of the actions.

Additional points about notifications:

* Multiple notifications can exist concurrently and will appear in chronological order from top to bottom.
* Notifications remain for the duration of the page instance or until the user dismisses or acts on them.
* Notifications that are defined on subpages, for example in parts and FatBoxes, appear in the same notification bar.
* Validation errors on the page will be shown first.

## Create and send notifications

To create a notification, you need to define a variable of data type **Notification**. Some properties and functions that you can use on a varaible of data type **Notification** are:

* **ID** - Specifies an identifier for the notification.
* **Message** - Specifies the content of the notification that appears in the UI.
* **Scope** - Specifies the scope in which the notification appears. You don't have to specify this property because the default is LocalScope.
* **Send** - Sends the notification to be displayed by the client.
* **AddAction** - Adds an action on the notification. By using the AddAction property, you can run a specific method in another codeunit.
* **SetData** - Sets a data property value for the notification.
* **GetData** - Gets a data property value from the notification.
* **Recall** - Recalls a sent notification.

If you specify an ID, you can recall this notification with the Recall function. Using an ID will also make sure that notifications with the same ID onl appear once in the list.

To send a basic notification, you need to provide a message and use the Send function. Be careful with the Message function; you can't use placeholders like you would on a regular Message statement. If you want to use placeholders, you need to use the StrSubstNo function to replace placeholders with their values.

```AL
var 
   MyNotification: Notification;
begin
   MyNotification.Id('2f511c20-b456-4894-8472-4eed76826c65');
   MyNotification.Message('Here comes your notification text.');
   MyNotification.Send();
end;
```

You can also add action to the notification. This feature allows the user to select the action and run a function in a codeunit. To send an action with a notification, use the AddAction function.

```AL
var
    MyNotification: Notification;
    ShowDetailsTxt: Label 'Show details';
begin
    MyNotification.Id('2f511c20-b456-4894-8472-4eed76826c65');
    MyNotification.Message('Here comes your notification text.');
    MyNotification.AddAction(ShowDetailsTxt, Codeunit::"Notif Mgmt", 'ShowInfo');
    MyNotification.Send();
end;

```
The following example shows how to create a global function in another codeunit that has a Notification data type parameter. You can add code to the function for handling the action.

```AL
codeunit 50102 "Notif Mgmt"
{
    procedure ShowInfo(MyNotification: Notification)
    begin
        Page.Run(Page::"Customer List");
    end;
}
```
You can also pass data to the Codeunit function. Therrefore, you can't pass the data as a parameter, but you need to use the SetData and GetData functions to add data to the notification. The SetData and GetData functions define the data as a key/value pair. The key/value pair can only accept text data types. If you want to pass a DateTime value or a Integer you first need to format the value.


```AL
var
    MyNotification: Notification;
    ShowDetailsTxt: Label 'Show details';
begin
    MyNotification.Id('2f511c20-b456-4894-8472-4eed76826c65');
    MyNotification.Message('Here comes your notification text.');
    MyNotification.SetData('Created', Format(CurrentDateTime, 0, 9));
    MyNotification.SetData('CustomerNo', '10000');
    MyNotification.AddAction(ShowDetailsTxt, Codeunit::"Notif Mgmt", 'ShowInfo');
    MyNotification.Send();
end;
```

The GetData function retrieves the data again by using the key.

```AL
codeunit 50102 "Custom Notification Management"
{
    procedure ShowInfo(MyNotification: Notification)
    var
        Customer: Record Customer;
        CustomerNo: Text;
        Created: DateTime;
    begin
        CustomerNo := MyNotification.GetData('CustomerNo');
        Evaluate(Created, MyNotification.GetData('Created'));

        if Customer.Get(CustomerNo) then
            Page.Run(Page::"Customer Card", Customer);
    end;
}
```















# Queries

## Introduction

A query translates into efficient SQL queries that are run on the Business Central database. A query can be used to export the data to an XML or CSV file, but it can also be used as a data source for APIs and Power BI reports.

## Get started with queries

The **Query** object of Business Central enables you to define relational data models that are trnaslated into efficient SELECT statements. SQL Server can run these statements as single data retrieval operations. This approach helps ensure optimal performance and minimal pressure on system resources.

Queries specify datasets from the Business Central database for retrieval in a fast and efficient way. You can use queries to retrieve data from one or more tables as a single, flat resul set. You can specify how to join multiple tables in the result set and how to order, group, aggregate, and filter the resulting data.

Queries retrieve data efficiently because they are always translated into a single SELECT statement and are run against the underlying SQL Server database. Data is selected, joined, grouped, ordered, aggregated, and filtered at the SQL Server level, which ensures a minimum effect on performance.

You can perform the following tasks with queries:

* Use them as sources of charts in Business Central.
* Save them as XML os CSV files (by using streams).
* Access them from AL code.
* Publish them as OData web services for later consumption for other clientes, such as Power BI or other applications in Microsoft Power Platform.

A query describes a dataset of Business Central data. Queries retrieve records from one or more tables and combine the records into rows and columns in a single dataset.

Queries have the following capabilities:

* Select subsets of fields from multiple tables.
* Join tables with different linking criteria.
* Filter tables by specifying filtering criteria.
* Group and aggregate data.
* Order data.
* Limit the number of rows to retrieve.
* Apply date methods.

### QueryType property

The QueryType property helps you define how the query will be used. This property has two possible values, Normal or API, where Normal is the default value. If you want to use a query in a Power BI report or access it by using an API call, you need to set the QueryTyppe property to API. Then, you can publish this query as a web service. The data from a query can only be viewed. 

Along with the QueryType property, you also need to set the APIPublisher, APIGroup, APIVersion, EntityName, and EntitySetName properties with an API query.

In APIs, the name of the data items, columns and filters should use camel casing. Instead of using CustomerNumber or Customer Number, you should use customerNumber.

### Join data in queries

The biggeest advantage of using queries comes from their ability to join data from multiple tables into a single resulting dataset. Therefore, you would usually model queries to include multiple data items that are joined to combine the data from multiple tables into a single resulting dataset.

You can join a data item to another data item by indenting it under another data item. The data item under which you have indented another data item becomes the parent data item. **Each parent data item can have only one child data item.**

When you indent data items under one another, you only specify that the data from one table will be joined to the data from another table. However, you have not defined how the data will be joined. To define the data joining rules, use the following data item properties:

* **DataItemLink -** Sets a reference between one or more fields of the source table of the current data item and fields of one or more source tables of the data item(s) that are higher in the data model hierarchy. You can link them by using the name of the data item.

Example from "Sales Header"
`DataItemLink = "Salesperson Code" = Salesperson.Code`

* **SqlJoinType -** Defines how the resulting dataset should handle the rows from the current data item if no match occurs between values that were specified in the data item link and data items that are higher in the data model hierarchy.

    * **CrossJoin -** Contains rows that combine each row from the upper data item table with each row from a lower data item table. Cross joins are also called Cartesian products. A cross join doesn't apply comparisons between columns of data items, so the DataItemLink property is left blank.

![SQLCrossJoin](/images/202412/SQL_Cross_Join.png)

* **FullOuterJooin -** Contains all records from the upper data item table and all records from the lower data item, including records that do not have matching value for columns that are linked by the DataItemLink property.

![SQLFullOuterJoin](/images/202412/SQL_FullOuterJoin.png)

* **InnerJoin -** Creates a dataset by combining records from data item tables where a match is found between the columns that are linked by the DataItemLink property of the lower data item. Inner join uses an equal to comparison operator to match rows from the lower data item table with rows fom the upper data item table that is based on the values of the linked columns.

![SQLInnerJoin](/images/202412/SQL_Inner_Join.png)

* **LeftOuterJoin -** Resembles the InnerJoin, except that the resulting dataset contains every record from the upper data item table, even if a record doesn't have a matching value in the lower data item for columns that are linked by the DataItemLink property.

![SQLLeftOuterJoin](/images/202412/SQL_Left_Outer_Join.png)

* **RightOuterJoin -** Resembles the InnerJoin, except that the resulting dataset contains every record from the lower data item table, even if a record does not have a matching value in the upper data item for columns that are linked by the DataItemLink property.

![SQLRightOuterJoin](/images/202412/SQL_Right_Outer_Join.png)

## Filter data in queries

Several ways to filter the data in resulting datasets of queries include: 

* Defining the DataItemTableFilter property on a data item.
* Defining the ColumnFilter property on a column.
* Creating items of type Filter.
* Writing AL code in the OnBeforeOpen trigger.

### DataItemTableFilter property

You can use the DataItemTableFilter property on data items to apply conditions on one or more fields of the table to limit the records in the resulting dataset of the query. You can filter on any field in the table, not just on those fields that are included as columns in the resulting dataset. The filters that you define in the DataItemTableFilter property are static; users or AL code can't override them.

```
        dataitem(Location; Location)
        {
            DataItemTableFilter = "Directed Put-away and Pick" = const(true);
            dataitem(Warehouse_Entry; "Warehouse Entry")
            {
            }
        }
```

### ColumnFilter property

You can use the ColumnFilter property on a column to apply a filter condition on a single field. The ColumnFilter property resembles the DataItemTableFilter property, but with some differences. Unlike filters that are set by the DataItemTableFilter property, filters that are set by the ColumnFilter property can be overwritten at run time by calling the SetFilter or SetRange functions from AL code.

If the ColumnFilter property specifies a filter on the same field as the DataItemTableFilter property, then the filters of the two properties are combined. To be included in the query dataset, records must meet the condition of both filters. For example, if the DataItemTableFilter property sets a filter on a field to include values less than fifty (<50), and the ColumnFilter property sets a filter on the same field to include values greater than twenty (>20), then the resultant filter on the field includes values that are greater than twenty and less than fifty.

The SetFilter and SetRange functions overwrite any filter on the same field that is set on a column or filter row by the ColumnFilter property. If a SetFilter or SetRange function filters on the same field as a filter on a data item, as specified by the DataItemTableFilter property, then the function filter and DataItemTableFilter property filter are combined.

```
    column(Sum_Qty_Base; "Qty. (Base)")
    {
        ColumnFilter = Sum_Qty_Base = filter(> 0);
    }
```

### Filter items

You can use filter items to enable dynamic filtering of the resulting dataset on fields that you don't want to include in the resulting dataset of a query.

For example, you might want to let users or developers filter on a date range; however, you do not want to include the date in the dataset. To define a filter, insert a new item in the data item for which you want to add a filter. Instead of adding a column, you can add a Filter, and then specify the field from the table on which you want to enable filtering.

```
    filter(Item_No; "Item No.")
    {
    }
    filter(Variant_Code; "Variant Code")
    {
    }
```

### OnBeforeOpen trigger 

The OnBeforeOpen trigger runs before its model is translated into a Transact-SQL statement and is run against the underlying SQL database.

You can use the OnBeforeOpen trigger to set filters on the query by using the SetFilter or SetRange AL functions. When you use AL, you can only filter the query on columns or filters that are defined in the query. You can't filter on any other fields that are present in any of the data items that you have not added to the query model as either a column or a filter.

`CurrQuery.SetFilter("Unit_Cost", '<>%1', 0);`

## Aggregate and sort data in a query

In a query, you can use a totals method to perform a calculation on a column and return the calculated value in the resulting dataset. This calculation is frequently called aggregation.

Typically, you would use aggregations with grouping to find totals for specified groups of columns. For example, you can sum the values in the Amount field for each G/L account in the G/L Entry table or find the Average Quantity per item in the Item Ledger Entry table. Totals methods in Business Central queries corresponde directly to SQL Server aggregate functions.

Following methods are available:

* **Sum -** Calculates the sum of the values of the specified column for all records that are grouped within the dataset.
* **Average -** Calculates the average value in the specified column in a group.
* **Min -** Retrieves the lowest value in the specified column in a group.
* **Max -** Retrieves the highest value in the specified column in a group.
* **Count -** Returns the number of records from data item tables that represent a group in the dataset.
```
    column(Sum_Qty_Base; "Qty. (Base)")
    {
        ColumnFilter = Sum_Qty_Base = filter(> 0);
        Method = Sum;
    }
```
You can order the data in a resulting dataset of a query by any number of columns that are contained in the query. To define the sorting order, set the OrderBy property on the query. To sort on multiple columns, separate each column with a comma.

```
query 7345 "Avail Qty. (Base) In QC Bins"
{
    Caption = 'Avail Qty. (Base) In QC Bins';
    OrderBy = descending(Sum_Qty_Base);
    ...
}
```

You can't sort by a column that is not present in the resulting dataset. If you want to order by such a column, you must first add it to the query model and then include it in the OrderBy property.

When retrieving Date or DateTime fields from a Business Central database, you might want to retrieve only the year, month, or day instead of the entire date. This notion is especially true when you group and total the data. For example, you might want to group revenue by customer and by year or find average consumption of a production BOM component by month. You can achieve this task by selecting a data method for a Date or DateTime column in the query. Date methods include **Day, Month and Year.**

When you use SQL server, date and time values are processed by using Coordinated Universal Time (UTC). If a Business Central solution uses a time zone other than UTC, and the field on which you apply the date method has a data type of DateTime, then a difference might occur between the date value that is returned in the dataset for the field and the actual day, month, or year for the field in the table. This situation occurs when the corresponding UTC date for a field falls on the next day or previous day because of the time of day and the time zone of Business Central.

The differences in day, month, or year occur when date and time values are retrieved from the Business Central database table. These values are converted from the regional settings of the Business Central solution to the UTC date and time. The day, month, or year is calculated on SQL server and then returned to the query dataset as an integer. **This integer does not consider the regional settings of a Business Central solution.**

To avoid this condition, you should use the date method on fields that have a Date data type instead of a DateTime data type, when possible. You can also return the DateTime value and implement post processing for the day, month, and year, as needed.



